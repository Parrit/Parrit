plugins {
    id "java"
    id "org.springframework.boot" version "2.0.3.RELEASE"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

repositories {
    mavenCentral()
}

dependencies {
    ext.commonsLang3Version = "3.7"

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-devtools"
    implementation "org.apache.commons:commons-lang3:$commonsLang3Version"

    runtimeOnly "org.postgresql:postgresql"
    runtimeOnly "org.flywaydb:flyway-core"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
}

version "0.0.1-SNAPSHOT"
sourceCompatibility = 1.8
targetCompatibility = 1.8

// *** LifeCycle Tasks *** //

defaultTasks "clean", "build"

task jsTest(type: Exec) {
    group = "Parrit"
    description = "Runs the frontend javascript tests"

    workingDir = "$project.projectDir/frontend"
    commandLine = "./node_modules/.bin/jest"
    args = ["-c", ".jestrc.json"]
}
test.dependsOn(jsTest)

task jsLint(type: Exec) {
    group = "Parrit"
    description = "Checks the javascript code syntax"

    workingDir = "$project.projectDir/frontend"
    commandLine = "./node_modules/.bin/eslint"
    args = ["--config", ".eslintrc.json", "."]
}
jsTest.dependsOn(jsLint)

task jsBuild(type: Exec) {
    group = "Parrit"
    description = "Bundles the frontend javascript code"

    workingDir = "$project.projectDir/frontend"
    commandLine = "./node_modules/.bin/webpack"
    args = ["--config", "webpack.config.js"]
}
processResources.dependsOn(jsBuild)

task jsDependencies(type: Exec) {
    group = "Parrit"
    description = "Downloads dependencies for the frontend javascript code"

    workingDir = "$project.projectDir/frontend"
    commandLine = "npm"
    args = ["install"]
}
jsBuild.dependsOn(jsDependencies)